set positional-arguments

db_dir := "db"
sqitch := "sqitch --chdir " + db_dir

default:
  @just --list

# Find a dotnet package and output the name and latest version
find-package-latest-version q:
  #!/usr/bin/env sh
  set -euo pipefail
  pkg=$(dotnet paket find-packages --silent --max 1 $1)
  ver=$(dotnet paket find-package-versions --silent --max 1 $1)
  echo $pkg $ver

# List all db targets
@db-targets:
  {{sqitch}} target

# Get the status of a db
@db-status TARGET:
  {{sqitch}} status {{TARGET}}

# Revert changes to a db
@db-revert TARGET:
  {{sqitch}} revert {{TARGET}}

# Deploy changes to a db
@db-deploy TARGET:
  {{sqitch}} deploy {{TARGET}}

# Create a db
db-create TARGET:
  #!/usr/bin/env bash
  set -euo pipefail
  cd {{db_dir}}
  uri=$(sqitch config --file sqitch.conf --get target.{{TARGET}}.uri | sed 's/.*://')
  mkdir -p $(dirname $uri)
  sqlite3 $uri "VACUUM;"

# Delete a db
db-delete TARGET:
  #!/usr/bin/env bash
  set -euo pipefail
  cd {{db_dir}}
  uri=$(sqitch config --file sqitch.conf --get target.{{TARGET}}.uri | sed 's/.*://')
  rm -f $uri
  registry=$(sqitch config --file sqitch.conf --get target.{{TARGET}}.registry)
  rm -f $(dirname $uri)/$registry

# Reset a db
@db-reset TARGET: (db-delete TARGET) (db-create TARGET) (db-deploy TARGET)

# Create a database migration
@db-migration-create TARGET +ARGS:
  {{sqitch}} add {{ARGS}} {{TARGET}}

# Build docker image for an application
@build-image TARGET:
  docker build -f src/{{TARGET}}/Dockerfile -t {{lowercase(TARGET)}} .
